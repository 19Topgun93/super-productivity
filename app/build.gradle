plugins {

    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.superproductivity.superproductivity"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 21
        versionName "21.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                hostName       : "app.super-productivity.com",
                defaultUrl     : "https://app.super-productivity.com",
                launcherName   : "Super Productivity",
                assetStatements: '[{ "relation": ["delegate_permission/common.handle_all_urls"], ' +
                        '"target": {"namespace": "web", "site": "https://app.super-productivity.com"}}]'
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    flavorDimensions "version"
    productFlavors {
        play {
            dimension "version"
            Properties properties = new Properties()
            file("google.properties").withInputStream {
                properties.load(it)
            }
            buildConfigField "String", "CLIENT_ID_WEB", "\"" + properties.webToken + "\""
        }
        fdroid {
            dimension "version"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace "com.superproductivity.superproductivity"
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "com.google.androidbrowserhelper:androidbrowserhelper:2.4.0"
    implementation "com.google.code.gson:gson:2.10"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "com.anggrayudi:storage:1.5.4"

    playImplementation "com.google.android.gms:play-services-auth:20.4.0"

    testImplementation "junit:junit:4.13.2"

    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
}
